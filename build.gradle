buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

def mcVersion = project.mc_version

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

version = project.version
group = "com.stevekung.modernsbsounds"
archivesBaseName = "ModernSkyblockSounds"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8

minecraft {
    version = "${forge_version}"
    runDir = "run"
    mappings = "${mcp_mapping}"
    makeObfSourceJar = false
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    embed("org.spongepowered:mixin:0.7.11-SNAPSHOT") { transitive = false }
}

repositories {
    maven {
        url = "https://repo.spongepowered.org/maven/"
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.modernsbsounds.refmap.json"
    }
}

jar {
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest {
        attributes("FMLAT": "modernsbsounds_at.cfg",
                   "FMLCorePluginContainsFMLMod": "true",
                   "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
                   "MixinConfigs": "mixins.modernsbsounds.json",
                   "ForceLoadAsMod": "true"
        )
    }
    baseName = "ModernSkyblockSounds-" + mcVersion
}